package FlamethrowerTower
import Tower
import TerrainUtils
import Projectile

public constant string FX_PATH = "Abilities\\Weapons\\PhoenixMissile\\Phoenix_Missile.mdl"//"Abilities\\Weapons\\LordofFlameMissile\\LordofFlameMissile.mdl"
public constant real AOE_RADIUS = 48.
public constant int FLAMETHROWER_ID = 'h01C'

function getDamage(int level) returns real
	return 7. + 4.75 * level
	
function getSpeed(int level) returns real
	return 6.25 + level * 1.5

public class FlamethrowerTower extends Tower
	int level = 1
	construct(Tower t)
		super(t)
		
	override function onAttack(UnitEntity ud)
		for i = 0 to level
			new FlameMissile(pos.withTerrainZ(58),ud.getPos().toVec2(), getSpeed(level), owner, level)
			
			
	override function onUpgrade()
		actor.issueImmediateOrderById(OrderId("stop"))
		level++
			
function isUnitTargetable(unit u) returns boolean
	return not IsUnitType(u, UNIT_TYPE_DEAD) and not IsUnitType(u, UNIT_TYPE_MAGIC_IMMUNE) and not IsUnitType(u, UNIT_TYPE_STRUCTURE) and not IsUnitType(u, UNIT_TYPE_FLYING)
		
class FlameMissile extends Projectile
	int level
	group affected
	
	construct(vec3 pos, vec2 target, real speed, player owner, int level)
		super( pos, AOE_RADIUS, owner, (pos.angleTo2d(target).radians + GetRandomReal(-0.175,0.175)).asAngleRadians(), FX_PATH )
		fx.setScale(0.2)
		setSpeed(speed + GetRandomReal(-1.5, 1.5))
		setRanged(712)
		setAcc(1.0175 + GetRandomReal(-0.005, 0.0075))
		fx.setColor(colorA(GetRandomInt(0,255),GetRandomInt(0,255),GetRandomInt(0,255),255))
		fx.setScale(0.75)
		this.level = level
		silent = true


	override function update()
		GroupEnumUnitsInRange(ENUM_GROUP, pos.x, pos.y, AOE_RADIUS, null)
		for u from ENUM_GROUP
			if IsUnitEnemy(u, owner) and isUnitTargetable(u)
				getDummy().damageTarget(u, getDamage(level), ATTACK_TYPE_SIEGE)
		super.update()
		
	
