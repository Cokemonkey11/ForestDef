package WaveTower
	import Tower
	import Projectile
	import TempGroups
	import Creep

	constant real AREA_OF_EFFECT = 200.
	public constant int FIRST_WAVE_ID = 'h01N'
	constant string FX_PATH = "Abilities\\Spells\\Other\\CrushingWave\\CrushingWaveMissile.mdl"
	
	public class WaveTower extends Tower
		int level = 1
		
		construct(Tower t)
			super(t)
			active = false
			
		override function onAttack(UnitEntity ud)
			new WaveMissile(pos,ud.pos.toVec2(), 20., owner, level)
			
		override function onUpgrade()
			level++
	
		
	function getDamage(int level) returns real//towerlevel
		return ((250. * level) + 50.)
	
	class WaveMissile extends Projectile
		int level
		group affected
		
		construct(vec3 pos, vec2 target, real speed, player owner, int level)
			super( pos, AREA_OF_EFFECT, owner, pos.angleTo2d(target), FX_PATH )
			setSpeed(30)
			setRanged(750.)
			affected = CreateGroup()
			this.level = level


		override function update()
			super.update()
			GroupEnumUnitsInRange(ENUM_GROUP, pos.x, pos.y, AREA_OF_EFFECT, null)
			for u in ENUM_GROUP
				let data = (u.getUserData() castTo UnitEntity)
				if data != null and data instanceof Creep and not IsUnitInGroup(u, affected)
					GroupAddUnit(affected, u)
					UnitDamageTarget(getDummy(), u, getDamage(level), false, false, ATTACK_TYPE_PIERCE, DAMAGE_TYPE_NORMAL, null)
					let ang = angleBetweenCoords(pos.x, pos.y, data.pos.x, data.pos.y)
					data.setVel(vec3(ang.cos() * (5 + level*2),ang.sin() * (5 + level*5),0))
			ENUM_GROUP.clear()
	
		ondestroy
			DestroyGroup(affected)



endpackage