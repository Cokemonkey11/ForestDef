package HealbackTower
import Tower
import Projectile
import TempGroups
import StompingTower
import Terrain

constant real AREA_OF_EFFECT = 50.
constant string HEAL_MISSILE_SFX = "Abilities\\Weapons\\FaerieDragonMissile\\FaerieDragonMissile.mdl"
public constant int HEAL_TOWER_ID = 'h01K'



public class HealAttackMissile extends Projectile
	real damage
	UnitEntity target
	HealbackTower parent
	real xyspeed

	construct(HealbackTower p, UnitEntity t,real damage, real speed)
		super(p.pos+vec3(0,0,160),AREA_OF_EFFECT, p.owner, p.pos.angleTo2d(vec2(t.pos.x,t.pos.y)), HEAL_MISSILE_SFX)
		this.damage = damage
		xyspeed = speed
		setSpeed(xyspeed)
		setRanged(1000.)
		target = t
		parent = p
		
	function setMissileFlyAngle()
		real distanceParab =distanceBetweenCoords(parent.pos.x,parent.pos.y,target.pos.x,target.pos.y)
		real distanceMissile =distanceBetweenCoords(pos.x,pos.y,target.pos.x,target.pos.y)
		real height = getParabolaHeight(0.,160.,250.,distanceParab,distanceMissile)
		angle zAngle = vec2(xyspeed,height-pos.z-getTerrainZ(pos.x,pos.y)).getAngle()
		pos.z = height
		
		setZAngle(zAngle)
		fx.setZ(pos.z-getTerrainZ(pos.x,pos.y))
		
	override function update()
		super.update()
		setMissileFlyAngle()
		if distanceBetweenCoords(pos.x,pos.y,target.pos.x,target.pos.y) <= 70.
			UnitDamageTarget(getDummy(), target.actor, 170., false, false, ATTACK_TYPE_PIERCE, DAMAGE_TYPE_NORMAL, null)
			new HealMissile(parent, target.pos,xyspeed,80.)
			destroy(this)


public class HealbackTower extends Tower
	int level
	
	construct(Tower t)
		super(t)
		
	override function onAttack(UnitEntity ud)
		new HealAttackMissile(this,ud, 20., 20.)
	
		
function getDamage(int level) returns real//towerlevel
	return ((30. * level) + 25.)/6

function isUnitTargetable(unit u) returns boolean
	return not IsUnitType(u, UNIT_TYPE_DEAD) and not IsUnitType(u, UNIT_TYPE_MAGIC_IMMUNE) and not IsUnitType(u, UNIT_TYPE_STRUCTURE)

class HealMissile extends Projectile
	HealbackTower parent
	real healing
	
	construct(HealbackTower p,vec3 pos, real speed, real healing)
		super( pos, AREA_OF_EFFECT, p.owner, pos.angleTo2d(p.pos.toVec2()), "Abilities\\Weapons\\PriestMissile\\PriestMissile.mdl" )
		parent = p
		this.healing = healing
		setSpeed(speed)
		setRanged(1000.)

	override function update()
		super.update()
		GroupEnumUnitsInRange(ENUM_GROUP, pos.x, pos.y, AREA_OF_EFFECT, null)
		for u in ENUM_GROUP
			let ud = u.getUserData() castTo UnitEntity
			if ud instanceof Wall
				ud.actor.setHP(ud.actor.getHP()+healing)
				destroy(this)
				//break
		ENUM_GROUP.clear()