package BoulderTower
import ClosureForGroups
import ATower
import PresetBuffs
import Interpolation


class BoulderBuff extends NormalBuff
	
	construct(BoulderTower bt)
		super(bt, 999999, 0, true)
		
	override function attackModifier()
		var tower = victim castTo BoulderTower
		// Only execute for one damage event
		if tower.newAttack
			forUnitsInRange(tower.lastTarget, tower.getSplashRadius(), (unit u) -> begin
				let targetData = u.getUserData() castTo UnitEntity
				let distanceSq = tower.lastTarget.distToVecSquared(targetData.getPos().toVec2())
				let percentageSplash = bezier_3(0, 0.3, 1, distanceSq / tower.getSplashRadius().squared())
				let angl = tower.lastTarget.angleTo(targetData.getPos().toVec2())
				targetData.addVel(vec3(angl.cos() * percentageSplash * 5,angl.sin() * percentageSplash * 5, 4 * percentageSplash))
			end)
			tower.newAttack = false
			
	
	override function defenseModifier()

class BoulderTower extends ATower
	int level = 0
	boolean newAttack = false
	vec2 lastTarget
	
	construct(ATower t)
		super(t)
		new BoulderBuff(this)
		
	override function onAttack(UnitEntity target)
		lastTarget = target.getPos().toVec2()
		newAttack = true
		
	function getSplashRadius() returns real
		return level * 50 + 100.
			
		
@towerdef function boulderDef()
	// Boulder 1-3
		new TowerObject(BOULDER_TOWER_ID1, "Boulder Tower Level 1", "Q")
		..setTooltip("The Boulder Tower comes with a radial knockback",
						"Splash & Knockback,Good vs Melee & Bosses", "Lower Attackspeed,Requires Micro,Weak vs Ranged & Magic")
		..setArt("", "ReplaceableTextures\\CommandButtons\\BTNRockTower.blp", 0.9, color(200,200,200), " ,second")
		..setCost(78, 0, 7, 15)
		..setDefense(550, 0, 6)
		..setAttack(AttackType.Siege, 50, 600, 2.5, 800, "Abilities\\Weapons\\AncientProtectorMissile\\AncientProtectorMissile.mdl", 0.35, false)
		..setSplash(100, 125, 175, 0.5, 0.25)
		..addUpgrade(BOULDER_TOWER_ID2)
		
		new TowerObject(BOULDER_TOWER_ID2, "Boulder Tower Level 2", "Q")
		..setTooltip("The first upgrade",
						"Splash & Knockback,Good vs Melee & Bosses", "Lower Attackspeed,Requires Micro,Weak vs Ranged & Magic")
		..setArt("", "ReplaceableTextures\\CommandButtons\\BTNRockTower.blp", 0.9, color(200,200,200), "second")
		..setCost(78, 0, 7, 15)
		..setDefense(550, 0, 6)
		..setAttack(AttackType.Siege, 50, 600, 2.5, 800, "Abilities\\Weapons\\AncientProtectorMissile\\AncientProtectorMissile.mdl", 0.35, false)
		..setSplash(100, 125, 175, 0.5, 0.25)
		..addUpgrade(BOULDER_TOWER_ID3)
		
		new TowerObject(BOULDER_TOWER_ID3, "Boulder Tower Level 3", "Q")
		..setTooltip("The second upgrade",
						"Splash & Knockback,Good vs Melee & Bosses", "Lower Attackspeed,Requires Micro,Weak vs Ranged & Magic")
		..setArt("", "ReplaceableTextures\\CommandButtons\\BTNRockTower.blp", 0.9, color(200,200,200), "second")
		..setCost(78, 0, 7, 15)
		..setDefense(550, 0, 6)
		..setAttack(AttackType.Siege, 50, 600, 2.5, 800, "Abilities\\Weapons\\AncientProtectorMissile\\AncientProtectorMissile.mdl", 0.35, false)
		..setSplash(100, 125, 175, 0.5, 0.25)
		
	