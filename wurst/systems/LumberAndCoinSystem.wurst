package LumberAndCoinSystem
import Creep
import StandardTextTags
import Assets

var WOOD_BOUNTY = 1
public constant COIN_ID = 'I00F'

var killcount = 0
var killcap = 18.

var coinCount = GetRandomInt(-2, 2)
var coinCap = 17
var coinsSpawned = 0

public function shouldSpawnCoin() returns boolean
	coinCount++
	if coinCount >= coinCap
		coinCount = 0
		coinCap += 3 + (coinsSpawned/2.).round()
		if bigArena
			coinCap += 3

		coinsSpawned++
		return true
	return false

function getExtraBounty()
	let victim = GetDyingUnit().getOwner()
	let killer = GetKillingUnit().getOwner()

	if victim == CREEP_OWNER and killer != CREEP_OWNER
		pDatas[killer.getId()].kills++
		killcount++
		if killcount >= killcap
			killcap += 1
			if bigArena
				killcap *= 1.25

			killcount = 0
			for pd in allPlayers
				pd.p.addLumber(WOOD_BOUNTY)
				if pd.builder.actor != null and pd.builder.actor.isAlive()
					createLumberBountyTextTag(pd.builder.actor, WOOD_BOUNTY)

function grantCoins()
	if GetManipulatedItem().getTypeId() == COIN_ID
		let gold = GetManipulatedItem().getUserData()
		flashEffect("UI\\Feedback\\GoldCredit\\GoldCredit.mdl", GetManipulatingUnit().getPos())
		printTimed("|cffFFCC00>> " + GetManipulatingUnit().getOwner().getNameColored() + " picked up a coin!", 10.)
		for p in allPlayers
			p.p.addGold(gold)
			if p.builder.actor != null and p.builder.actor.isAlive()
				createGoldBountyTextTag(p.builder.actor, gold)
				flashEffect(Other.resourceeffecttarget, p.builder.getPos())
			
public function initLumberSystem()
	if allPlayers.getSize() <= 2
		WOOD_BOUNTY = 2
	if bigArena
		killcap = 36
		coinCap = 54 + GetRandomInt(-6,6)
	EventListener.add(EVENT_PLAYER_UNIT_DEATH, () -> getExtraBounty())
	EventListener.add(EVENT_PLAYER_UNIT_PICKUP_ITEM, () -> grantCoins())