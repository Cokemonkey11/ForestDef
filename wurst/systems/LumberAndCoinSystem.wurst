package LumberAndCoinSystem
import Creep
import Assets

constant WOOD_REDISTRIBUTE_THRESHOLD = 25

var WOOD_BOUNTY = 1
public constant COIN_ID = 'I00F'
public constant WOOD_ID = 'I00H'

var killcount = 0
var killcap = 18.

var coinCount = GetRandomInt(-2, 3)
var coinCap = 16
var coinsSpawned = 0

public function shouldSpawnCoin() returns boolean
	coinCount++
	if coinCount >= coinCap
		coinCount = 0
		coinCap += 4 + GetRandomInt(-1, 2) + (coinsSpawned/4.).round()
		if bigArena
			coinCap += 2

		coinsSpawned++
		return true
	return false

function getExtraBounty()
	let victim = GetDyingUnit().getOwner()
	let killer = GetKillingUnit().getOwner()

	if victim == CREEP_OWNER and killer != CREEP_OWNER
		pDatas[killer.getId()].kills++
		killcount++
		if killcount >= killcap
			killcap += 1
			if bigArena
				killcap += 3
				killcap *= 1.015

			killcount = 0
			let itm = createItem(WOOD_ID, GetDyingUnit().getPos())
			doAfter(45.) ->
				if itm != null and itm.getLife() > .405
					itm.remove()

			for pd in allPlayers
				if pd.p.getLumber() > WOOD_REDISTRIBUTE_THRESHOLD and allPlayers.size() > 1
 					let val = (pd.p.getLumber() / allPlayers.size()).toInt()
					let remainder = pd.p.getLumber() mod allPlayers.size()
					pd.p.setLumber(remainder)
					for p2 in allPlayers
						p2.p.addLumber(val)
					printTimedToPlayer("Due to inactivity your lumber has been dispersed among teammates!", 10, pd.p)

function grantCoinsAndLumber()
	let itm = GetManipulatedItem()
	if itm.getTypeId() == COIN_ID
		let gold = itm.getUserData()
		flashEffect(UI.goldCredit, GetManipulatingUnit().getPos())
		printTimed("|cffFFCC00>> " + GetManipulatingUnit().getOwner().getNameColored() + " picked up a coin!", 10.)
		for p in allPlayers
			p.p.addGold(gold)
			if p.builder.actor != null and p.builder.actor.isAlive()
				createFText(p.builder.getPos() - vec2(16,0), "+" + gold.toString(), 0.024 / 0.0023)
					..setDynamic(vec2(0., .03), 3.)
					.tt..setFadepoint(2.)
						..setColor(255, 220, 0, 255)
				flashEffect(Abilities.resourceEffectTarget, p.builder.getPos())
	if itm.getTypeId() == WOOD_ID
		flashEffect(Objects.entBirthTarget, GetManipulatingUnit().getPos())
		printTimed("|cffFFCC00>> " + GetManipulatingUnit().getOwner().getNameColored() + " picked up a lumber bundle!", 10.)
		for pd in allPlayers
			pd.p.addLumber(WOOD_BOUNTY)
			if pd.builder.actor != null and pd.builder.actor.isAlive()
				createFText(pd.builder.getPos() - vec2(16,0), "+" + WOOD_BOUNTY.toString(), 0.024 / 0.0023)
					..setDynamic(vec2(0., .03), 3.)
					.tt..setFadepoint(2.)
						..setColor(0, 200, 80, 255)
			if pd.p.getLumber() > 20 and allPlayers.size() > 1
				let val = (pd.p.getLumber() / allPlayers.size()).toInt()
				let remainder = pd.p.getLumber() mod allPlayers.size()
				pd.p.setLumber(remainder)
				for p2 in allPlayers
					p2.p.addLumber(val)
				printTimedToPlayer("Due to inactivity your lumber has been dispersed among teammates!", 10, pd.p)


public function initLumberSystem()
	if allPlayers.size() <= 2
		WOOD_BOUNTY = 2
	if bigArena
		killcap = 32
		coinCap = 54 + GetRandomInt(-6,6)
	EventListener.add(EVENT_PLAYER_UNIT_DEATH, () -> getExtraBounty())
	EventListener.add(EVENT_PLAYER_UNIT_PICKUP_ITEM, () -> grantCoinsAndLumber())