package LumberAndCoinSystem
import Creep
import StandardTextTags
import GameTimer
import Assets

var WOOD_BOUNTY = 1
public constant COIN_ID = 'I00F'

var killcount = 0
var killcap = 18.

var coinCount = 0
var coinCap = 17
var coinsSpawned = 0

public function shouldSpawnCoin() returns boolean
	coinCount++
	if coinCount >= coinCap
		coinCount = 0
		coinCap += 7 + coinsSpawned
		if bigArena
			coinCap += 11

		coinsSpawned++
		return true
	return false

function getExtraBounty()
	let victim = GetDyingUnit().getOwner()
	let killer = GetKillingUnit().getOwner()

	if victim == CREEP_OWNER and killer != CREEP_OWNER
		pDatas[killer.getId()].kills++
		killcount++
		if killcount >= killcap
			killcap += 1
			if bigArena
				killcap += 1

			killcount = 0
			for pd in allPlayers
				pd.p.addLumber(WOOD_BOUNTY)
				createLumberBountyTextTag(pd.builder.actor, WOOD_BOUNTY)

function grantCoins()
	if GetManipulatedItem().getTypeId() == COIN_ID
		let gold = GetManipulatedItem().getUserData()
		for p in allPlayers
			p.p.addGold(gold)
			createGoldBountyTextTag(p.builder.actor, gold)
			flashEffect(Other.resourceeffecttarget, p.builder.getPos())
			
public function initLumberSystem()
	if allPlayers.getSize() <= 2
		WOOD_BOUNTY = 2
	if bigArena
		killcap = 36
		coinCap = 54 + GetRandomInt(-4,4)
	EventListener.add(EVENT_PLAYER_UNIT_DEATH, () -> getExtraBounty())
	EventListener.add(EVENT_PLAYER_UNIT_PICKUP_ITEM, () -> grantCoins())
