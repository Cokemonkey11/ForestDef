package Selector
import Entity
import Builder
import PlayerData
import Rocket
import Assets
import SoundUtils
import ClosureTimers
import Wave
import Interpolation

public constant FAT_ID = 'h022'
public constant SLIM_ID = 'h00F'
public constant FAT_SPELL_ID = 'A01Y'
public constant SLIM_SPELL_ID = 'A01X'
constant PUDDING_ID = 'I00B'
constant ENERGY_ID = 'I00C'

public class Selector extends UnitEntity
	FText ownerTag
	
	vec3 finalPos
	vec3 mid
	real p = 0.

	construct(vec3 ppos, player owner)
		super(createUnit(owner, 'h023', middle.toVec3(), angle(0)))
		ownerTag = createFText(ppos - vec3(20,25,0), owner.getNameColored(), 12.5)
		actor.setVertexColor(fromPlayerColor(owner.getColor()).withAlpha(255))
		finalPos = ppos
		mid = middle.polarOffset(middle.angleTo(finalPos.toVec2()) + (48).asAngleDegrees(), 168).toVec3()
		if GetLocalPlayer() == owner
			SelectUnit( actor, true )
			PanCameraToTimed(actor.getX(), actor.getY(), 0)

		EventListener.add(actor, EVENT_PLAYER_UNIT_SPELL_CAST, () -> begin
			let id = GetSpellAbilityId()
			let pd = pDatas[owner.getId()]
			if id == FAT_SPELL_ID
				pd.builder = new Builder(actor.getPos3Zero(), pd, FAT_ID)
				pd.fat = true
				pd.builder.actor.addItem(PUDDING_ID)
				if owner.getName() == "curryking" or owner.getName() == "crigges" or owner.getName().toLowerCase() == "waterknight"
					let eff = addEffect(Other.plaguecloudtarget, pd.builder.getPos())
					doAfter(5, () -> eff.destr())
					snd.playForPlayer(owner)
			else if id == SLIM_SPELL_ID
				pd.builder = new Builder(actor.getPos3Zero(), pd, SLIM_ID)
				pd.fat = false
				pd.builder.actor.addItem(ENERGY_ID)
			terminate()
			pd.select = null
			pd.builder.actor.addItem(ROCKET_ITEM_ID)
		end)

	override function update()
		if p > 0.990
			deactivate()
			return
		p = p.lerp(1, 0.025)
		pos = pos.bezier3(mid, finalPos, p)
		actor.setPos(pos.toVec2())
		ownerTag.tt.setPos(pos - vec3(20,25,0))
		owner.selectSingle(actor)

	ondestroy
		actor.remove()
		destroy ownerTag
		pDatas[owner.getId()].select = null

SoundDefinition snd = new SoundDefinition("Units\\Creeps\\Ogre\\OgrePissed5.wav", false)
