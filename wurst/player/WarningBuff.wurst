package WarningBuff
import PresetBuffs
import SoundUtils
import TextTagEntity
import GameTimer
import initlater PlayerData
import initlater Selector
import Assets

SoundDefinition snd = new SoundDefinition("Abilities\\Spells\\Other\\Silence\\Silence1.wav", false)

public buffTuple warningBuffAbil = compiletime(createBuffObject("|cff00ff00Warning", "Be cautious", "BTNWellSpring.blp"))

public class WarningBuff extends NormalBuff
	real startedTime
	real lastWarning
	real lastSelect

	boolean dead = false
	player originalOwner
	
	construct()
		super(9999, warningBuffAbil)
		startedTime = currentTime
		lastWarning = currentTime
		lastSelect = currentTime
		
	override function apply(UnitEntity target) 
		super.apply(target)
		originalOwner = target.owner
	
	override function defenseModifier()
		let dmg = getDamage()
		let pos = target.getPos()
		if startedTime + 15. > currentTime
			new TextTagEntity(target.getPos()-vec3(-75,0,0), vec3(GetRandomReal(-2.5, 2.5),0, GetRandomReal(8,10)), "Spawn-Protection" , 10, 1, colorA(155, 185, 55, 255))
			addEffect("Abilities\\Spells\\Items\\SpellShieldAmulet\\SpellShieldCaster.mdl", target.getPos() ).destr()
			modifyDamage(0)
		if lastWarning + 1. < currentTime and target.actor.getHP() - dmg < (target.actor.getMaxHP()/1.5)
			new TextTagEntity(target.getPos()-vec3(20,0,0), vec3(GetRandomReal(-1, 1),GetRandomReal(-1, 1), GetRandomReal(7,12)), "ATTENTION!!" , 15, 1, colorA(255, 25, 25, 255))
			snd.playForPlayer(target.owner)
			lastWarning = currentTime
		if lastSelect + 5. < currentTime and target.actor.getHP() - dmg < (target.actor.getMaxHP()/2.)
			PanCameraToTimedForPlayer(target.owner, pos.x, pos.y, 0.25)
			target.owner.selectSingle(target.actor)
			lastSelect = currentTime

		if dmg > target.actor.getHP()
			modifyDamage(0)
			if not dead
				dead = true
				let u = target.actor
				let fat = pDatas[target.owner.getId()].fat
				u..setHP(1.)
				..setOwner(DUMMY_PLAYER, false)
				..addAbility('Avul')
				..pause()
				..hide()..setTimeScale(0.0001)
				createUnit(originalOwner, fat ? FAT_ID : SLIM_ID, u.getPos(), u.getFacingAngle())..setXY(u.getPos()).kill()
				let owner = target.owner
				pDatas[owner.getId()].alive = false
				pDatas[owner.getId()].deaths++
				printTimed("|cffFFCC00>>|r " + owner.getNameColored() + "  |cffCE2119has been killed!", 20)
		
	override function refresh(boolean forceRefresh)
		super.refresh(forceRefresh)
		if dead
			let fat = pDatas[target.owner.getId()].fat
			target.actor
			..unpause()
			..setTimeScale(fat ? 0.25 : 1.0)
			..setHP(99999)
			..show()
			..setAnimation("stand")
			..queueAnimation("stand")
			..removeAbility('Avul')
			..setOwner(originalOwner, false)
			target.owner..selectSingle(target.actor)
			..panCamToTimed(target.actor, 0.)
			flashEffect(Other.revivehuman, target.actor.getPos())
			dead = false

		