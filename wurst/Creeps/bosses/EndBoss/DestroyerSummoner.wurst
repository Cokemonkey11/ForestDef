package DestroyerSummoner
import Creep

constant int SUMMONER_ID = 'u016'
constant int DESTROYER_ID = 'n00L'
constant int SUMMONER_BOUNTY = 99
constant int DESTROYER_BOUNTY = 666
constant real DESTROYER_SUMMON_TIME = 30.
constant real updatePeriod = .05
constant real animationLoopPeriod = 45.

public class FinalBossSummoning
	real castPercent = 0
	real animationLoop = 0.
	int castingCount = 0
	LinkedList<DestroyerSummoner> summoners
	
	texttag summonText
	
	construct()
		summoners = new LinkedList<DestroyerSummoner>()
		summonText = CreateTextTag()
		summonText..setPos(middle.x, middle.y, 100)..setText("0%", 12)
		summonText.setVisibility(false)
		doPeriodically(updatePeriod, (CallbackPeriodic cb) -> this.update())

	function update()
		var newCastingCount = 0
		for summoner in summoners
			if summoner.getPos().distanceTo2d(middle) < 220. and not summoner.casting
				startCasting(summoner)
				newCastingCount ++
			if summoner.casting
				if summoner.actor.isAlive()
					newCastingCount++
				else
					summoners.remove(summoner)
		castingCount = newCastingCount
		castPercent += getSummonSpeed(castingCount) * (updatePeriod * 100 / DESTROYER_SUMMON_TIME)
		print( getSummonSpeed(castingCount) * (updatePeriod * 100 / DESTROYER_SUMMON_TIME))
		if castPercent < 0
			castPercent = 0
		if castPercent >= 100
			castPercent = 100
			summonDestroyer()
		else
			this.updateAnimation()

	function updateAnimation()
		animationLoop = (animationLoop + updatePeriod / animationLoopPeriod) % 1.
		summonText.setVisibility(true)
		summonText.setText(castPercent.toInt().toString() + "%", 12)
		summonText.setColor(PercentTo255(castPercent), 255 - PercentTo255(castPercent), 0, 255)
		var i = 0
		for summoner in summoners
			if summoner.casting
				summoner.setPos(middle.polarOffset(angle((animationLoop + (i - castingCount) / castingCount) * 6.28), 200.).toVec3().add(0, 0, 120))
				summoner.actor.setFacing(summoner.pos.angleTo2d(middle))
				i++

	function startCasting(DestroyerSummoner summoner)
		summoner.sleeps = true
		summoner.casting = true
		// summoner.actor.setFacing(summoner.pos.angleTo2d(middle))
		// summoner.actor.setAnimation("attack spell")
		summoner.actor.queueAnimation("attack spell")


	function getSummonSpeed(int castingCount) returns real
		if castingCount <= 0
			return -3
		return castingCount / 4 + castingCount * castingCount / 4

	function addSummoner(DestroyerSummoner summoner)
		summoners.add(summoner)

	function summonDestroyer()
		new Creep(middle.polarOffset(angle(6.28), 300.).toVec3(), DESTROYER_ID, DESTROYER_BOUNTY, middle)
		destroy this

	ondestroy
		for DestroyerSummoner s in summoners
			s.actor.kill()
		destroy summoners
		summonText.destr()

public class DestroyerSummoner extends Creep
	boolean casting
	// which position to stand in once casting
	int position

	construct(vec3 pos, int position)
		super(pos, SUMMONER_ID, SUMMONER_BOUNTY, middle)
		this.position = position

	override function onDeath()
		super.onDeath()
		casting = false
