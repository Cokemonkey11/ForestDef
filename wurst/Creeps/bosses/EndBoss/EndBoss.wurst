package EndBoss
import TowerBlockade
import DestroyerSummoner
import Tower
import Boss
import EndGame

constant ENDBOSS_ID = 'n00P'

constant m = new Music('0000', "Sound\\Music\\mp3Music\\Doom.mp3")
constant finalBossMaxHp = 1000
constant castInterval = 8.
constant stageTwoPercentage = .33
constant stageThreePercentage = .10

public EndBossFight currentEndBossFight

public class EndBoss extends Boss
	int stage = 1
	int castCounter = 0
	timer castTimer
	LinkedList<string> abilities = new LinkedList<string>()
	EndBossFight bossFight

	construct(vec3 pos, int typId, int bounty, vec2 target, EndBossFight bossFight)
		super(pos, typId, bounty, target)
		abilities.add("blockade")
		abilities.add("destroyerSummoner")
		castTimer = getTimer()
		castTimer.setData(this castTo int)
		castTimer.startPeriodic(castInterval, function EndBoss.onCastTimer)
		this.bossFight = bossFight

	override function update()
		super.update()
		let percentage = actor.getHP() / finalBossMaxHp
		if stage == 1 and percentage < stageTwoPercentage
			stage = 2
	// 		abilities.remove("blockade")
	// 		abilities.add("raiseDead")
	// 		abilities.add("shield")
		if stage == 2 and percentage < stageThreePercentage
			stage = 3
	// 		abilities.remove("shiled")
	// 		abilities.add("batteringRam")

	static function onCastTimer()
		let boss = GetExpiredTimer().getData() castTo EndBoss
		boss.castCounter++
		switch boss.stage
			case 1
				if boss.castCounter mod (1 * startPosCount) == 0
					boss.castAbility()
			case 2
				if boss.castCounter mod (2 * startPosCount) == 0
					boss.castAbility()
			case 3
				boss.castAbility()

	function castAbility()
		let abilName = abilities.get(GetRandomInt(0, abilities.size() - 1))
		// print("Final Boss Casting - " + abilName)
		switch abilName
			case "blockade"
				tryCastTowerBlockade()
			case "destroyerSummoner"
				castDestroyerSummoner()
			default
				print("Ability not implemented :)))")

	function tryCastTowerBlockade()
		GroupEnumUnitsInRange(ENUM_GROUP, pos.x, pos.y, 3072., Filter(() -> begin
			var u = GetFilterUnit()
			return IsUnitType(u, UNIT_TYPE_STRUCTURE) and u.isAlive() and u.getEntity() instanceof Tower and pDatas[u.getOwner().getId()] != null
		end))
		for tower in ENUM_GROUP
			let towerEntity = tower.getEntity()
			var hasBuff = false
			if towerEntity.hasBuffs()
				for bff in towerEntity.getBuffs()
					if bff instanceof TowerBlockadeBuff
						hasBuff = true
			if not hasBuff
				castTowerBlockade(towerEntity castTo Tower)
				return

	function castTowerBlockade(Tower tower)
		actor.setAnimation("spell")
		// new TowerBlockadeMissile(pos, owner, tower) // causes instant crash
		doAfter(BLOCKADE_MISSILE_TIME) ->
			new TowerBlockadeBuff(tower)

	function castDestroyerSummoner()
		currentEndBossFight.createDestroyerSummoner(this)

	// TODO function castShield()
	// TODO function castRaiseDead()
	// TODO function switchPlaces()

	override function onDeath()
		super.onDeath()
		bossFight.onBossDeath(this)

	ondestroy
		destroy this.abilities
		castTimer.destr()

public class EndBossFight
	LinkedList<EndBoss> bosses = new LinkedList<EndBoss>()
	FinalBossSummoning destroyerSummoning

	boolean summoningComplete = false

	construct()
		PlayMusic(m.path)
		// new SoundDefinition(Sounds.airBargeDeath1, false).play()
		print("|cffE62121 Prepare for your end!")
		destroyerSummoning = new FinalBossSummoning(this)
		for j = 0 to startPosCount
			bosses.add(new EndBoss(spawnRects[j].randomPoint().toVec3(), ENDBOSS_ID, 175, middle, this))

	function createDestroyerSummoner(EndBoss castingBoss)
		if not summoningComplete
			let summoner = new DestroyerSummoner(castingBoss.getPos())
			destroyerSummoning.addSummoner(summoner)

	function endDestroyerSummoning()
		if not summoningComplete
			summoningComplete = true
			for boss in bosses
				boss.abilities.remove("destroyerSummoner")
			destroy destroyerSummoning

	function onBossDeath(EndBoss b)
		bosses.remove(b)
		if bosses.size() == 0
			destroy this

	ondestroy
		endDestroyerSummoning()
		destroy bosses
		onVictory()

public function startEndBoss()
	printTimed("    |cffFFC519Beat the final boss to win!", 25)
	currentEndBossFight = new EndBossFight()

public function onVictory()
	printTimed("|cffFFCC00>> |cffFFA53ACongratulations, you won!|r\n        |cff7B7B7BThe game will end in 20 seconds.", 25)
	winGame()
	doAfter(20) ->
		for pd in allPlayers
			CustomVictoryBJ(pd.p, true, false)

class Music
	int spellid
	string path
	real duration
	string name

	construct(int id, string path)
		this.spellid = id
		this.path = path
		this.duration = GetSoundFileDuration(path) / 1000.
		this.name = path.substring(path.lastIndexOf("\\") + 1, path.lastIndexOf("."))
