package EndBoss
import TowerBlockade
import TextTagEntity
import Tower
import Buff
import Boss

constant ENDBOSS_ID = 'n00P'

constant m = new Music('0000', "Sound\\Music\\mp3Music\\Doom.mp3")

public class EndBoss extends Boss
	var castInterval = 30.

	construct(vec3 pos, int typId, int bounty, vec2 target)
		super(pos, typId, bounty, target)
		doPeriodically(this.castInterval, (CallbackPeriodic cb) -> begin
			print("Casting Blockade")
			// switch to random ability, add conditions for 
			this.tryCastTowerBlockade()
		end)

	function tryCastTowerBlockade()
		GroupEnumUnitsInRange(ENUM_GROUP, pos.x, pos.y, 1000., Filter(() -> begin
			var u = GetFilterUnit()
			return IsUnitType(u, UNIT_TYPE_STRUCTURE) and u.isAlive() and pDatas[u.getOwner().getId()] != null and u.getEntity() instanceof Tower
		end))
		for tower in ENUM_GROUP
			let towerEntity = tower.getUserData() castTo UnitEntity
			var hasBuff = false
			if towerEntity.hasBuffs()
				for bff in towerEntity.getBuffs()
					if bff.typeId == this.typeId
						hasBuff = true
			if not hasBuff
				castTowerBlockade(tower.getUserData() castTo Tower)
				return

	function castTowerBlockade(Tower tower)
		new TowerBlockadeMissile(pos, owner, tower)
		actor.setAnimation("spell")
		doAfter(BLOCKADE_MISSILE_TIME, () -> begin
			new TowerBlockadeBuff(tower)
			new TextTagEntity(pos, vec3(GetRandomReal(-1, 1),0, GetRandomReal(6,8)), "Blockaded!", 8, 1., colorA(145, 80, 12, 255))
		end)

	// TODO function castDestroyerSummoner()
	// TODO function castShield()
	// TODO function castWave()
	// TODO function switchPlaces()

public class EndBossFight
	LinkedList<EndBoss> bosses

	construct()
		PlayMusic(m.path)
		PlaySoundBJ(gg_snd_AirBargeDeath1)
		print("|cffE62121 Prepare for your end!")
		bosses = new LinkedList<EndBoss>()
		for j = 0 to 0 //startPosCount
			bosses.add(new EndBoss(spawnRects[j].randomPoint().toVec3(), ENDBOSS_ID, 175, middle))

	ondestroy
		destroy bosses
		onVictory()

	
let bossWave = new BossWave(ENDBOSS_ID, 1, 175, gg_snd_AirBargeDeath1, (vec3 startPos, int unitId, int bounty, vec2 middle) -> new EndBoss(startPos, unitId, bounty, middle))

public function startEndBoss()
	printTimed("    |cffFFC519Beat the final boss to win!", 25)
	new EndBossFight()

public function onVictory()
	printTimed("|cffFFCC00>> |cffFFA53ACongratulations, you won!|r\n        |cff7B7B7BThe game will end in 20 seconds.", 25)
	doAfter(20, () -> begin
		for pd in allPlayers
			CustomVictoryBJ(pd.p, true, false)
	end)



class Music
	int spellid
	string path
	real duration
	string name
	
	construct(int id, string path)
		this.spellid = id
		this.path = path
		this.duration = GetSoundFileDuration(path) / 1000.
		this.name = path.substring(path.lastIndexOf("\\") + 1, path.lastIndexOf("."))
