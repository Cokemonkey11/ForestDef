package BossTwo
import Boss
import ShieldBuff

constant TENTACLE_ID = 'u00W'
constant TENTACLE_BUFF_ID = 'A020'

public class SludgeBuff extends NormalShieldBuff
	private color currentCol = color(155,255,155)
	private color nextCol = color(155,255,155)
	private real dmgHold = 0

	construct(UnitEntity victim, real dur, int abilId)
		super(victim, dur, abilId, 6500)
		currentCol = color(155,255,155)
		
	override function update()
		super.update()
		currentCol = currentCol.lerp(nextCol, 0.35)
		victim.actor.setVertexColor(currentCol)
		
	override function attackModifier()
		
	override function defenseModifier()
		dmgHold += getDamage()
		if dmgHold > 500
			dmgHold -= 500
			nextCol = color(GetRandomInt(25,255),GetRandomInt(25,255),GetRandomInt(25,255))
			new Tentacle(victim.getPos().offset2d(victim.actor.getFacingAngle() + angle(GetRandomReal(-0.75, 0.75)), GetRandomReal(64, 148)))

public class Tentacle extends Creep
	
	construct(vec3 pos)
		super(pos, TENTACLE_ID, 7, middle)
		actor.setPropWindow(0)

public class BossTwo extends Boss

	construct(vec3 pos, int typId, int bounty, vec2 target)
		super(pos, typId, bounty, target)
		new SludgeBuff(this, 99999, TENTACLE_BUFF_ID)

