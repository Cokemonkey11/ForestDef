package Entity
import LinkedListModule
import Terrain
import PhysicsConstants
import Fx
import Angle
import initlater Buff
import public LinkedList
import public DmgMod
import MapBounds
import Event
import InstanceBoard
		
/** Base Entity Class  */
public abstract class Entity
	use LinkedListModule
	use ShowInstances
	/** 3d vector position */
	vec3 pos
	/** 3d vector velocity */
	vec3 vel
	
	/** radius */
	real radius = 0.
	/** speed factor (multiplies to added velocity) */
	real speedFactor = 1.
	
	/** owning player */
	player owner
	
	/** flag for being static */
	boolean isStatic 	= false
	/** sleeping or active? */
	boolean active 		= true
	boolean flying 		= false
	boolean done 		= false
	
	override static function classname() returns string
		return "Entity"

	construct( vec3 pos, real radius, player owner )
		this.pos = pos
		this.vel = vec3(0,0,0)
		this.owner = owner
		this.radius = radius
	
	construct( vec3 pos, vec3 vel, real radius, player owner )
		this.pos = pos
		this.vel = vel
		this.owner = owner
		this.radius = radius
		
	function getPos() returns vec3
		return pos
		
	function setPos( vec3 pos )
		this.pos = pos
		
	function addPos( vec3 pos )
		this.pos += pos
		
	function getVel() returns vec3
		return vel
		
	function setVel( vec3 vel )
		this.vel = vel
		active = true
	
	function addVel( vec3 vel )
		this.vel += vel
		
	function setTarget( vec3 tpos, real speed )
		var t = distanceBetweenCoords(pos.x, pos.y, tpos.x, tpos.y) / speed
		let tangle = angleBetweenCoords(pos.x, pos.y, tpos.x, tpos.y)
		let e = getTerrainZ(tpos.x,tpos.y)
		
		if t < 1.
			t = 1./speed
		
		let startZVelocity = ((-gravity.z * t) / 2 - pos.z/t + e/t)
		this.setVel( vec3( Cos(tangle.radians()) * speed, Sin(tangle.radians()) * speed, startZVelocity) )
		
	/** This function is called every ANIMATION_PERIOD tick if the Entity is active */
	abstract function update()
	
abstract public class UnitEntity extends Entity
	unit actor
	
	private LinkedList<Buff> currentBuffs
	
	construct( vec3 pos, real radius, unit actor )
		super(pos, radius, actor.getOwner())
		this.actor = actor
		actor..setUserData(this castTo int)..addAbility(HEIGHT_ENABLER).removeAbility(HEIGHT_ENABLER)
		registerUnit(actor)
		
	function getBuffs() returns LinkedList<Buff>
		if done
			return null
		if currentBuffs == null
			currentBuffs = new LinkedList<Buff>()
		return currentBuffs
		
	function hasBuffs() returns boolean
		return currentBuffs != null
		
	override function update()
		updatePos()
		
	function updatePos()
		pos = vec3(actor.getX(), actor.getY(), 0.)
		
	function setNewActor(unit u)
		actor.remove()
		actor = u
		actor.setUserData(this castTo int)
		
	override function setPos(vec3 tpos)
		pos = tpos
		if(pos.inPlayable())
			actor.setPos(tpos.x,tpos.y)
			actor.setFlyHeight(tpos.z - getTerrainZ(tpos.x, tpos.y), 0)
		else 
			done = true
			
	override function getPos() returns vec3
		updatePos()
		return pos
		
	function setXY(vec3 tpos)
		pos = tpos
		actor.setXY(tpos.x,tpos.y)

	function onDeath(unit killer)
		done = true
			
	ondestroy
		if hasBuffs()
			for bff in currentBuffs
				bff.victim = null
				bff.done = true
			destroy currentBuffs
		
		
abstract public class FxEntity extends Entity
	protected Fx fx
	boolean silent = false
	
	construct( vec3 pos, real radius, player owner, angle xyAngle, string sfxpath )
		super(pos, radius, owner)
		fx = new Fx(pos, xyAngle, sfxpath)
		fx.setOwner(owner, true)
		
	construct( vec3 pos, real radius, player owner, angle xyAngle )
		super(pos, radius, owner)
		fx = new Fx(pos, xyAngle)
		fx.setOwner(owner, true)
	
	construct( vec3 pos, player owner, angle xyAngle )
		super(pos, 0., owner)
		fx = new Fx(pos, xyAngle)
		fx.setOwner(owner, true)
		
	override function update()
		pos += (vel*speedFactor)
		setPos()
		
	function setFx(string sfxpath)
		fx.setFx(sfxpath)
		
	function setPos()
		if(pos.inPlayable())
			fx.setXYZ(pos)
		else 
			done = true
		
	function getDummy() returns unit
		return fx.getDummy()
		
	override function setPos(vec3 tpos)
		pos = tpos
		fx.setPos(tpos.x,tpos.y)
		
	ondestroy
		debugPrint("fx destroyed", 1)
		if silent
			fx.hiddenDestroy()
		else
			destroy fx





	