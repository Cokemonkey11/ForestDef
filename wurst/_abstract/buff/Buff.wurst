package Buff

import public Entity
import InstanceBoard
import LinkedListModule
import Event
import Squares
import TextTagEntity

public constant BUFF_ANIM_PERIOD = 0.25

public abstract class Buff
	use ShowInstances
	use LinkedListModule
	UnitEntity victim

	real duration
	int abilId
	boolean done = false
	boolean applied = false
	
	override static function classname() returns string
		return "Buff"

	construct(UnitEntity victim, real dur, int abilId, boolean apply)
		this.victim = victim
		this.abilId = abilId
		this.duration = dur
		if apply
			apply()

	function update()
		if duration <= 0.
			done = true
		else
			duration -= BUFF_ANIM_PERIOD
		
		
	function refresh() 
		if not victim.actor.hasAbility(abilId)
			victim.actor.addAbility(abilId)
			
	ondestroy
		if applied and victim != null and not victim.done
			victim.getBuffs().remove(this)
			boolean isLast = true
			for bff in victim.getBuffs()
				if bff.typeId == this.typeId
					isLast = false
					break
			if isLast
				victim.actor.removeAbility(abilId)
			
	abstract function apply()

	abstract function attackModifier()
	
	abstract function defenseModifier()

@setup function initBuff()
	EventListener.add(EventType.ON_DAMAGE, () -> begin
		let v = GetTriggerUnit()
		let a = GetEventDamageSource()
		if a.isAliveTrick() and a.getUserData() > 0 and v.isAliveTrick() and v.getUserData() > 0
			let victimData = v.getUserData() castTo UnitEntity
			let attackerData = a.getUserData() castTo UnitEntity
			if IsPlayerAlly(victimData.owner, attackerData.owner) or victimData.owner == attackerData.owner
				// Falsify teamattacks
				if getDamage() > 0.5
					new TextTagEntity(victimData.getPos(), vec3(GetRandomReal(-2,2),GetRandomReal(-2,2),GetRandomReal(8,10)), getSquare(), 10, .8
					,colorA(GetRandomInt(150,250),GetRandomInt(125,150),GetRandomInt(125,150), 255))
					if attackerData.hasBuffs()
						for bff in attackerData.getBuffs()
							if not bff.done
								bff.attackModifier()
					modifyDamage(0)
			else
				// Correct attack, apply Buffs
				if victimData != null and victimData.hasBuffs()
					for bff in victimData.getBuffs()
						if not bff.done
							bff.defenseModifier()
				if attackerData != null and attackerData.hasBuffs()
					for bff in attackerData.getBuffs()
						if not bff.done
							bff.attackModifier()
	end)							
	EventListener.add(EventType.ON_DEATH, () -> begin
		let v = GetDyingUnit()
		if v.getUserData() > 0
			let victimData = v.getUserData() castTo UnitEntity
			victimData.onDeath(GetKillingUnit())
	end)
